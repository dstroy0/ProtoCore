// Copyright (C) 2026 Douglas Quigg (dstroy0) <dquigg123@gmail.com>
// SPDX-License-Identifier: AGPL-3.0-or-later

/**
 * @file Templating.ino
 * @brief Response templating with {{name}} placeholder substitution.
 *
 * send_template() streams a template, replacing each `{{name}}` token with the
 * value returned by a TemplateVar resolver (nullptr -> empty). The body is
 * walked twice (size, then write) so it is never buffered whole - memory use is
 * constant regardless of page size. Unknown / over-long / unterminated
 * placeholders are emitted literally.
 *
 * Flash, open Serial @ 115200 for the IP, then:
 *   curl http://<ip>/
 */

#include "protocore.h"
#include "network_drivers/physical/physical/physical.h"

static const char *SSID = "YOUR_SSID";
static const char *PASSWORD = "YOUR_PASSWORD";


static unsigned long hit_count = 0;

// Resolver: map a placeholder name to its replacement. Returned pointers must
// stay valid for the duration of the send_template() call; the resolver is
// invoked twice (sizing pass + write pass) so it must be deterministic.
static const char *resolver(const char *name)
{
    static char buf[32];
    if (strcmp(name, "title") == 0)
    {
        return "Templating Demo";
    }
    if (strcmp(name, "uptime") == 0)
    {
        snprintf(buf, sizeof(buf), "%lu", millis());
        return buf;
    }
    if (strcmp(name, "hits") == 0)
    {
        snprintf(buf, sizeof(buf), "%lu", hit_count);
        return buf;
    }
    if (strcmp(name, "heap") == 0)
    {
        snprintf(buf, sizeof(buf), "%u", ESP.getFreeHeap());
        return buf;
    }
    return nullptr; // unknown -> empty
}

// GET / - render an HTML page from a template.
void handle_root(uint8_t slot_id, HttpReq *req)
{
    (void)req;
    hit_count++;
    static const char page[] = "<!doctype html><html><body>"
                               "<h1>{{title}}</h1>"
                               "<p>uptime: {{uptime}} ms</p>"
                               "<p>hits: {{hits}}</p>"
                               "<p>free heap: {{heap}} bytes</p>"
                               "</body></html>";
    send_template(slot_id, 200, "text/html", page, resolver);
}

void setup()
{
    Serial.begin(115200);

    Physical.wifi->init(SSID, PASSWORD);
    Serial.print("Connecting to WiFi");
    while (!Physical.wifi->ready())
    {
        delay(250);
        Serial.print('.');
    }
    uint32_t ip = Physical.link->egress_ip(); // library egress IP (network byte order), no Arduino WiFi
    Serial.printf("\nIP: %u.%u.%u.%u\n", (unsigned)(ip & 0xFF), (unsigned)((ip >> 8) & 0xFF),
                  (unsigned)((ip >> 16) & 0xFF), (unsigned)((ip >> 24) & 0xFF));

    on_http("/", HTTP_GET, handle_root);

    int32_t result = begin_http(80, NULL);
    if (result < 0)
    {
        Serial.printf("begin() failed (error %d)\n", result);
        return;
    }
    Serial.println("Server started on port 80");
}

void loop()
{
    handle();
}
